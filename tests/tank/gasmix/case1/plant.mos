/*  A simple script Template to run my Plant Model
          Author: Ravi Saripalli
	  Date:   14 Jan. 2015
       
      No need to stick user library in OpenModelica Library
     
*/  

/***************     Load Library and Models   *********/
echo(true) ;
print(getSettings());

ThermoLib := getEnvironmentVar("THERMOS") + "/package.mo";
if(loadFile(ThermoLib)) then
   print ("ThermoS Loaded\n");
else
   print("ThermoS Load Failed\n" + getErrorString());
   exit(1);
end if;

if(loadFile("plant.mo")) then
   print ("plant Loaded\n");
else
   print(getErrorString());
   print("plant Load Failed\n" + getErrorString());
   exit(1);
end if;

print("Saving Total Model in total.mo \n") ;
saveTotalModelDebug("total.mo",plant); // gives stand alone mo file to send out for debug reports.
print(getErrorString());

//ThermoS.Util.getInfo();  // Gives me the list of all switches in omc
print(checkModel(plant));  // This should pickup flattening errors
/************ Cleanup Work **********/
print("Cleaning Up Work Area\n") ;
system("rm work/*"); cd("./work");   system("pwd");


// *****  Simulation / translation ******
tEnd := 500; iniStepSize := 1  ;
count := integer(floor(tEnd / iniStepSize)) ;

setDebugFlags("infoXmlOperations");
flags :=  ""; // "-lv=LOG_NLS " ;  

/**    Dot Touch this section  else you lose diagnostics**/
print("Starting the Plant \n");
echo(true); // you need both echo and getErrorString() weird ...
simulate(plant, startTime=0, outputFormat="csv", 
         stopTime=tEnd,  // tolerance=1e-6, 
         cflags="-ffloat-store", simflags=flags) ;
getErrorString();
echo(false);
/**    Dot Touch this section  **/

// *** Check Simulation run/translation failures  ***
sVars :=  readSimulationResultVars("plant_res.csv") ; //print(sVars);
if (size(sVars,1) == 0) then  // no variables then bailout
  print("simulation failed ... bailing out\n");
  exit(1);
else
   print("simulation done\n");
   print("No. of Plant Variables = " + String(size(sVars,1)) + "\n");
end if;

getErrorString();
plot(tank.pFull,
       curveWidth=2, externalWindow=true, title = "%Full");
system("sleep 1s");
plot(tank.level,
       curveWidth=2, externalWindow=true, title = "level(m)");
system("sleep 1s");
plot(tank.Tf,
       curveWidth=2, externalWindow=true, title = "Temp. (C)");
system("sleep 1s");
